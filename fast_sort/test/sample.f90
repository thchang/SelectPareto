PROGRAM TEST_ARGSORT_DVEC
! Driver code that tests the function ARGSORT_DVEC from FAST_SORT, using
! data generated by VTMOP.
!
! The inputs are read from sample.dat and the sorted outputs are printed to
! stdout.

! Import the FAST_SORT types and interfaces.
USE FAST_SORT
IMPLICIT NONE

! Output file.
CHARACTER(LEN=20), PARAMETER :: INFILE = "sample.dat"

! Adjust the problem dimensions below. D is the dimension of each vector,
! and N is the number of vectors.
INTEGER, PARAMETER :: D=3, N=1926

! Local variables
INTEGER(KIND=INT64) :: I
LOGICAL :: ERROR

! Allocate inputs
REAL(KIND=REAL64), DIMENSION(D, N) :: VALUES, VALUES_OLD
INTEGER(KIND=INT64), DIMENSION(N) :: INDICES
FORALL (I = 1:N) INDICES(I) = I

! Read the inputs from INFILE.
OPEN(99, FILE=INFILE)
DO I = 1, N
   READ(99,*) VALUES(:, I)
END DO
CLOSE(99)

! Make a copy of values for checking later.
VALUES_OLD(:, :) = VALUES(:, :)

! Sort the inputs.
CALL ARGSORT_DVEC(VALUES, INDICES)

! Check VALUES for out-of-order-type errors.
ERROR = .FALSE.
DO I = 2, N
   IF (SUM(VALUES(:, I)) < SUM(VALUES(:, I-1))) THEN
      ERROR = .TRUE.; EXIT
   END IF
END DO
IF (ERROR) THEN
   WRITE(*,*) "An error has occurred in ARGSORT_DVEC: ", &
              "VALUES was not properly sorted."
! VALUES is properly sorted; check that the INDICES are a correct permutation.
ELSE
   IF (ALL(VALUES_OLD(:, INDICES) .EQ. VALUES(:, :))) THEN
      WRITE(*,*) "ARGSORT_DVEC test has passed"
   ELSE
      WRITE(*,*) "An error has occurred in ARGSORT_DVEC: ", &
                 "The INDICES appear to be incorrect"
   END IF
END IF

END PROGRAM TEST_ARGSORT_DVEC
